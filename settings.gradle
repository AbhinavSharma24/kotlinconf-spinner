rootProject.name = "kotlin-native-demo"

def konanBuildPath = gradle.startParameter.projectProperties['konan.build']
if (konanBuildPath != null) {
    includeBuild(konanBuildPath)
    gradle.allprojects {
        it.extensions.extraProperties.set('konan.home', "$konanBuildPath/dist")
    }
}

def disableAndroid = gradle.startParameter.projectProperties['disable.android']
if (disableAndroid == null || disableAndroid != "true") {

    if (System.getenv("ANDROID_HOME") != null || new File(rootProject.projectDir, "local.properties").isFile()) {
        include ':clients:android'
    } else {
        println "WARN. Android SDK may not be configured, excluding Android project..."
    }
}

include ':clients:cli'
include ':clients:ios'
//include ':clients:webassembly'
include ':common'
include ':getopt'
include ':httpserver'
include ':json'
include ':kurl'
include ':sql'
include ':utils'


enableFeaturePreview('GRADLE_METADATA')
